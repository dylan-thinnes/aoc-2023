#!/usr/bin/env -S sed -Ef
# Initialize sum in hold space
1{x;s/.*/0/;x;}

# Extract first and second digit
s/^[^0-9]*//
s/[^0-9]*$//
s/^(.).*(.)$/\1\2/
s/^(.)$/\1\1/

G;s/\n/:/

# Interleave with hold space, which contains running total
:loop1
s/(.):([0-9]*)([0-9])/:\2#\1\3/
s/(.):#/:#\1#/
s/^:([0-9]*)([0-9])#/:\1#\2#/
t loop1

s/://

# Add interleaved value
s/0//g
s/1/@/g
s/2/@@/g
s/3/@@@/g
s/4/@@@@/g
s/5/@@@@@/g
s/6/@@@@@@/g
s/7/@@@@@@@/g
s/8/@@@@@@@@/g
s/9/@@@@@@@@@/g

:loop2
s/(^|#)@@@@@@@@@@/@#/g
t loop2

s/@@@@@@@@@/9/g
s/@@@@@@@@/8/g
s/@@@@@@@/7/g
s/@@@@@@/6/g
s/@@@@@/5/g
s/@@@@/4/g
s/@@@/3/g
s/@@/2/g
s/@/1/g
s/^#//g
s/#$/#0/g
:loop3
s/##/#0#/g
t loop3
s/#//g

# Save into hold space
h
